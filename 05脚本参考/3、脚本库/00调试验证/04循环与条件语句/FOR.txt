*** Settings ***
Documentation     FOR循环结构如下：
...
...               :FOR 循环变量 IN RANGE \ end
...
...               :FOR 循环变量 IN RANGE start end step
...
...               循环体为接下来的缩进代码
...
...               或
...
...               :FOR 循环变量 IN \ a <b,c,...>
...
...               循环体为接下来的缩进代码
Library           Selenium2Library

*** Test Cases ***
IN RANGE
    @{A}    Set Variable    a    b    c
    ${local_rownum}    Evaluate    len(${A})    #获得数组参数的个数3
    #循环三次
    : FOR    ${local_I}    IN RANGE    ${local_rownum}    #循环执行时是从0~${local_rownum}-1
    \    log    ${A[${local_I}]}
    #循环两次
    : FOR    ${local_I}    IN RANGE    len(${A})-1    #循环执行时是从0~${local_rownum}-1
    \    log    ${A[${local_I}]}

IN RANGE_STEP
    @{A}    Set Variable    a    b    c    d
    ${local_rownum}    Evaluate    len(${A})    #获取数组行数，数组默认是从0行0列开始
    #正（0~3）
    : FOR    ${local_I}    IN RANGE    0    len(${A})    1    #最后一个变量是步序
    \    LOG    ${A[${local_I}]}
    #负（4~1）
    : FOR    ${local_I}    IN RANGE    len(${A})    0    -1
    \    LOG    ${A[${local_I}-1]}

IN
    #将数组变更元素做为遍历对象
    @{A}    Set Variable    a    b
    : FOR    ${local_I}    IN    @{A}
    \    LOG    ${local_I}
    #直接指定遍历内容
    : FOR    ${local_I}    IN    a    b    c
    \    LOG    ${local_I}

双重循环
    #FOR循环语句不能嵌套，如果需要写双重循环，只能调用循环关键字的方式
    : FOR    ${I}    IN RANGE    1    10
    \    LOG    ${I}
    \    FORJ

Exit For Loop
    #立即停止执行FOR循环
    @{SOME LIST}    Set Variable    aaa    bbb    EXIT    DDD
    : FOR    ${var}    IN    @{SOME LIST}
    \    Run Keyword If    '${var}' == 'EXIT'    Exit For Loop    #满足条件时退出循环
    \    LOG    ${var}

Exit For Loop IF
    #满足条件时停止执行FOR循环
    @{SOME LIST}    Set Variable    aaa    bbb    EXIT    DDD
    : FOR    ${var}    IN    @{SOME LIST}
    \    Exit For Loop If    '${var}' == 'EXIT'    #满足条件时退出循环
    \    LOG    ${var}

*** Keywords ***
FORJ
    : FOR    ${J}    IN RANGE    1    3
    \    LOG    ${J}
