*** Settings ***
Documentation     封装通用操作
Library           Selenium2Library
Library           AutoItLibrary
Library           OperatingSystem
Resource          ../资源文件/通用封装(all_included).txt

*** Variables ***

*** Keywords ***
登录
    [Arguments]    ${userName}    ${password}
    [Documentation]    *功能：*
    ...
    ...    登录ERP系统
    ...
    ...    *参数说明：*
    ...
    ...    userName : 用户名
    ...
    ...    password ：登录密码
    ...
    ...    url : ERP系统地址（在通用参数配置配置）
    ...
    ...    companyPath：登录之后选择公司，父子路径之间用/分割（在通用参数配置配置）
    ...
    ...    *示例：*
    ...
    ...    登录 | admin | 1
    open browser    ${url}    chrome    remote_url=http://192.168.236.3:4444/wd/hub
    Execute Javascript    window.checkValidateCode=function(){return true;};    #屏蔽验证码检测
    input text    user    ${userName}
    input password    pwd    ${password}
    click button    submit
    Comment    Wait Until Keyword Succeeds    10s    100ms    选择公司    ${companyPath}

选择公司
    [Arguments]    ${companyPath}
    [Documentation]    *功能：*
    ...
    ...    登录之后，在弹出的“选择公司”页面，选择某个公司
    ...
    ...    *参数说明：*
    ...
    ...    companyPath: 公司层次路径，父级与子级之间用"/"分割
    ...
    ...    *示例：*
    ...
    ...    选择公司 总部/华南区域/深圳公司
    sleep    1s
    ${status}    Run Keyword And Return Status    select window    title=选择公司
    Return From Keyword If    '${status}' == 'False'
    select frame    ctl_iframe_show
    Map_SingleSelectTree_双击行    Table1    ${companyPath}
    Unselect Frame

模块导航
    [Arguments]    ${functionPath}
    [Documentation]    *功能：*
    ...
    ...    左侧功能模块菜单导航
    ...
    ...    *参数说明：*
    ...
    ...    functionPath : 用"/"分割的子模块路径
    ...
    ...    *示例：*
    ...
    ...    模块导航 | 项目运营管理(POM)/成本管理/合同订立/合同登记
    定位页面    title=明源地产ERP
    Execute Javascript    window.onbeforeunload=window.onunload=function(){};    #屏蔽关闭窗口事件
    Wait Until Page Contains Element    nav1    10s
    select frame    nav1
    ${functionArr}    evaluate    u'${functionPath}'.split('/')
    ${functionArrLen}    evaluate    len(${functionArr})
    : FOR    ${i}    IN RANGE    2
    \    ${functionName}    evaluate    ${functionArr}[${i}]
    \    Wait Until Page Contains Element    xpath=//td[text()='${functionName}']    10s
    \    run keyword if    ${i}==0    Execute Javascript    var tables=window.document.getElementsByTagName('table');for(var table in tables){if(tables[table].innerText=='${functionName}' && tables[table].id.indexOf('tblNav')>-1){if(tables[table].innerHTML.indexOf('nav_arrow_down.gif')>-1)break;tables[table].click();break;}}
    \    ...    ELSE    click element    xpath=//tr[@id='trBar']//td[text()='${functionName}']
    Wait Until Page Contains Element    frmFuncNav    10s
    select frame    frmFuncNav
    : FOR    ${i}    IN RANGE    2    ${functionArrLen}
    \    ${functionName}    evaluate    ${functionArr}[${i}]
    \    Wait Until Page Contains Element    xpath=//td[text()='${functionName}']
    \    run keyword if    ${i}==2    Execute Javascript    var tables=window.document.getElementsByTagName('table');for(var table in tables){if(tables[table].innerText=='${functionName}' && tables[table].id.indexOf('tblNav')>-1){if(tables[table].innerHTML.indexOf('nav_arrow_down.gif')>-1)break;tables[table].click();break;}}
    \    ...    ELSE    _ModuleNavClickLastMenu    ${functionName}
    unselect frame

退出
    [Documentation]    *功能：*
    ...
    ...    关闭ERP系统
    定位页面    title=明源地产ERP
    Select Frame    menu1
    点击主菜单项    文件/关闭    #点击：文件/关闭
    sleep    500ms
    Run Keyword And Ignore Error    confirm action

切换用户登录
    [Arguments]    ${UserName}    ${PassWord}    ${companyPath}
    [Documentation]    *功能：*
    ...
    ...    ERP系统操作过程中，同一场景下不同的功能会由不同岗位人员进行操作。如成本专员新增合同，成本部经理审核合同，在流程操作过程中，需要切换用户登录系统
    ...
    ...    *参数说明：*
    ...
    ...    userName : 用户名
    ...
    ...    password ：登录密码
    ...
    ...    *示例：*
    ...
    ...    登录 | admin | 1
    定位页面    title=明源地产ERP
    Select Frame    menu1
    点击主菜单项    文件/重新登录
    Wait Until Keyword Succeeds    10s    500ms    select window    url=%default.aspx?RemoveSession=0%
    Input Text    txtUserCode    ${UserName}
    Input Text    txtPsw    ${PassWord}
    click button    btnLogin
    Wait Until Keyword Succeeds    10s    100ms    选择公司    ${companyPath}

登录至指定模块
    [Arguments]    ${UserName}    ${PassWord}    ${companyPath}    ${functionPath}
    [Documentation]    *功能：*
    ...
    ...    用户登录后导航至对应模块
    ...
    ...    ERP系统操作过程，登录系统后通常要切换到某个功能模块，为简化脚本，将这两步进行整体封装
    ...
    ...    *参数说明：*
    ...
    ...    userName : 用户名
    ...
    ...    password ：登录密码
    ...
    ...    functionPath : 用"/"分割的子模块路径
    ...
    ...    *示例：*
    ...
    ...    登录至指定模块 | admin | 1 | 项目运营管理(POM)/成本管理/合同订立/合同登记
    登录    ${UserName}    ${PassWord}    ${companyPath}
    模块导航    ${functionPath}

切换用户登录至指定模块
    [Arguments]    ${UserName}    ${PassWord}    ${companyPath}    ${functionPath}
    [Documentation]    *功能：*
    ...
    ...    切换用户登录后导航至对应模块
    ...
    ...    ERP系统操作过程中，同一场景下不同的功能会由不同岗位人员进行操作。如成本专员新增合同，成本部经理审核合同，在流程操作过程中，需要切换用户登录系统然后再导航至待操作模块
    ...
    ...    *参数说明：*
    ...
    ...    userName : 用户名
    ...
    ...    password ：登录密码
    ...
    ...    functionPath : 用"/"分割的子模块路径
    ...
    ...    *示例：*
    ...
    ...    切换用户登录至指定模块 | admin | 1 | 项目运营管理(POM)/成本管理/合同订立/合同登记
    切换用户登录    ${UserName}    ${PassWord}    ${companyPath}
    模块导航    ${functionPath}

切换二级模块
    [Arguments]    ${functionPath}
    [Documentation]    *功能：*
    ...
    ...    切换二级模块，仅适用于在当前子系统下切换功能模块
    ...    如需至其它子系统进行操作，需重新登录后通过模块导航切入
    ...
    ...    *参数说明：*
    ...
    ...    functionPath : 用"/"分割的子模块路径
    ...
    ...    *示例：*
    ...
    ...    切换二级模块 | 交易管理/认购管理
    ...
    ...    *注意：*
    ...
    ...    只有当当前模块处于二级模块时才可以使用该关键字
    定位页面    title=明源地产ERP
    unselect frame
    select frame    nav1/frmFuncNav
    ${functionPathArr}    evaluate    u'${functionPath}'.split('/')
    Execute Javascript    var tables=window.document.getElementsByTagName('table');for(var table in tables){if(tables[table].innerText=='${functionPathArr[0]}' && tables[table].id.indexOf('tblNav')>-1){if(tables[table].innerHTML.indexOf('nav_arrow_down.gif')>-1)break;tables[table].click();break;}}
    _ModuleNavClickLastMenu    ${functionPathArr[1]}
    unselect frame

切换公司
    [Arguments]    ${BUNamePath}
    [Documentation]    *功能：*
    ...
    ...    通过ERP主菜单栏的工具/切换公司来切换至新公司
    ...
    ...    *参数说明：*
    ...
    ...    BUNamePath: 层次路径，父级与子级之间用"/"分割
    ...
    ...    *示例：*
    ...
    ...    切换公司 | 总部/华南区域/深圳公司
    定位页面    title=明源地产ERP
    Select Frame    menu1
    点击主菜单项    工具/切换公司    #点击：工具/切换公司
    Unselect Frame
    Wait Until Keyword Succeeds    10s    500ms    select window    title=选择公司
    select Frame    ctl_iframe_show
    Map_SingleSelectTree_选中行    Table1    ${BUNamePath}    #SingleSellectTree_所属Table的id|SingleSellectTree_层次路径
    click button    btnOk    #确 定
    unselect Frame

切换用户登录（新导航）
    [Arguments]    ${UserName}    ${PassWord}
    [Documentation]    *功能：*
    ...
    ...    ERP系统操作过程中，同一场景下不同的功能会由不同岗位人员进行操作。如成本专员新增合同，成本部经理审核合同，在流程操作过程中，需要切换用户登录系统
    ...
    ...    *参数说明：*
    ...
    ...    userName : 用户名
    ...
    ...    password ：登录密码
    ...
    ...    *示例：*
    ...
    ...    登录 | admin | 1
    定位页面    title=明源地产ERP
    Click Element    menu-logout
    定位页面    title=明源地产ERP
    登录    ${UserName}    ${PassWord}

模块导航（新导航）
    [Arguments]    ${SystemName}    ${companyPath}    ${pathArr_index0}    ${pathArr_index1}
    [Documentation]    注释：
    ...
    ...    ${SystemName}：子系统名称
    ...
    ...    ${companyPath}：公司路径
    ...
    ...    ${pathArr_index0}：一级模块名称
    ...
    ...    ${pathArr_index1}：二级模块名称
    sleep    3s
    定位页面    title=明源地产ERP
    Wait Until Keyword Succeeds    10s    100ms    Element Should Be Visible    dd_sysNav
    Click Element    //div[@id='dd_sysNav']
    Click Element    //div[@id='system-menu']//dt/a[text()='${SystemName}']    #点击系统导航
    选择公司    ${companyPath}    #公司路径
    定位页面    title=明源地产ERP
    Comment    ${pathArr_index0}    Evaluate    u'${Path}'.split('/')[0]
    Comment    ${pathArr_index1}    Evaluate    u'${Path}'.split('/')[1]
    Run Keyword And Ignore Error    Click Element    //a[@title="${pathArr_index0}"]
    Run Keyword And Ignore Error    Click Element    //span[@title="${pathArr_index1}"]
    Execute Javascript    var allNodes = document.getElementsByTagName('span');var i;var node;var tagTitle='${pathArr_index1}';for(i=0;i<allNodes.length;i++){node=allNodes[i];if(node.title){if(node.title==tagTitle){node.click();}}}    #防止没点到的情况

登录test
    [Arguments]    ${userName}    ${password}
    [Documentation]    *功能：*
    ...
    ...    登录ERP系统
    ...
    ...    *参数说明：*
    ...
    ...    userName : 用户名
    ...
    ...    password ：登录密码
    ...
    ...    url : ERP系统地址（在通用参数配置配置）
    ...
    ...    companyPath：登录之后选择公司，父子路径之间用/分割（在通用参数配置配置）
    ...
    ...    *示例：*
    ...
    ...    登录 | admin | 1
    open browser    ${url}    chrome
    Execute Javascript    window.checkValidateCode=function(){return true;};    #屏蔽验证码检测
    input text    xpath=//*[@id='userid']/input    ${userName}
    input password    xpath=//*[@id='password']/input    ${password}
    click button    xpath=//*[@class='el-form-item login-submit-item']/div/button
