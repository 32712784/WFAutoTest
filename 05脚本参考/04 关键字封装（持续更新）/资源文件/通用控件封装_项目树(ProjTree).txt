*** Settings ***
Library           Selenium2Library
Resource          通用工具方法.txt

*** Keywords ***
Map_ProjTree_选择项目
    [Arguments]    ${projectNameList}    ${divLocator}=//div[@class='ProjTree']
    [Documentation]    *功能：*
    ...
    ...    选择项目树复选框
    ...
    ...    *参数说明：*
    ...
    ...    projectNameList ： 项目名称集合，每个项目名称包含父路径与子路径，父子路径之间用/分割；对于多个项目，项目与项目之间用|分割
    ...
    ...    divLocator :
    ...    ProjTree控件对应的DIV元素的定位信息，此参数具有默认值，一般可以省略，
    ...    但是如果定位信息与默认值不同时，则必须填写此参数
    ...
    ...    *示例：*
    ...
    ...    Map_ProjTree_选择项目 | 绿景中心/一期|绿景中心/二期
    Return From Keyword If    '${projectNameList}'=='[NULL]' or '${projectNameList}'==''
    #检查上下文
    _myExecuteJS    var divs=window.document.getElementsByTagName('div');for(var i=0;i<divs.length;i++){if(divs[i].className&&divs[i].className.indexOf('ProjTree')>-1){return;}}return'0|||封装提示信息：未找到cssClass为ProjTree的div元素，请检查是否进入了正确的iframe或者使用了正确的关键字'
    ${divId}    Get Element Attribute    ${divLocator}@id
    ${projectNameArr}    Evaluate    u'${projectNameList}'.split('|')
    ${projectNameArrLen}    Evaluate    len(${projectNameArr})
    :FOR    ${i}    IN RANGE    ${projectNameArrLen}
    \    ${projectName}    Evaluate    u'${projectNameArr[${i}]}'
    \    _selectProjTree    ${projectName}    ${divId}

_selectProjTree
    [Arguments]    ${projectName}    ${divId}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    设置项目数选项
    ...
    ...    *参数说明：*
    ...
    ...    projectName: 项目名称，父路径与子路径之间用/分割
    ...
    ...    divId ：ProjTree控件对应的DIV元素的id
    ${projectNameLevelArr}    Evaluate    u'${projectName}'.split('/')
    ${projectNameLevelArrLen}    Evaluate    len(${projectNameLevelArr})
    ${j}    Evaluate    int(${projectNameLevelArrLen})-1
    :FOR    ${k}    IN RANGE    ${projectNameLevelArrLen}
    \    ${currentProjectNameLevel}    Evaluate    '-'.join(${projectNameLevelArr}[0:${k}+1])
    \    Run Keyword If    ${k} < ${j}    _expandProjTreeMidLevel    ${currentProjectNameLevel}    ${divId}    ELSE
    \    ...    select checkbox    //div[@id='${divId}']//table//tr[@LevelFullName='${currentProjectNameLevel}']//input[1]

_expandProjTreeMidLevel
    [Arguments]    ${currentProjectNameLevel}    ${divId}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    展开中间层级
    ${handText}    get text    //div[@id='${divId}']//table//tr[@LevelFullName='${currentProjectNameLevel}']//span[text()='[+]' or text()='[-]']
    run keyword if    '${handText}' == '[+]'    click element    //div[@id='${divId}']//table//tr[@LevelFullName='${currentProjectNameLevel}']//span[text()='[+]']
