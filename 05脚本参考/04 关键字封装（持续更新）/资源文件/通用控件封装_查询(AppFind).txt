*** Settings ***
Library           Selenium2Library
Resource          通用工具方法.txt

*** Keywords ***
Map_AppFind_普通查找
    [Arguments]    ${findBy}    ${value}    ${isQueryInView}=No
    [Documentation]    *功能：*
    ...
    ...    普通查找
    ...
    ...    *参数说明：*
    ...
    ...    findBy : 按哪个字段查找
    ...
    ...    value : 查找关键字
    ...
    ...    isQueryInView ：是否视图内查询 (Yes - 是 ，No - 否 ，默认值为No)
    ...
    ...    *示例：*
    ...
    ...    Map_AppFind_普通查找 | 合同名称 | 公司库存合同-结算-工作流审批01
    Return From Keyword If    '${value}'=='' or '${value}'=='[NULL]'
    Wait Until Page Contains Element    __appFind_switch_standard    5s
    click element    __appFind_switch_standard
    run keyword if    '${isQueryInView}'=='Yes'    select checkbox    __appFind_queryinview
    Select From List By Label    __appFind_field    ${findBy}
    input text    __appFind_textbox    ${value}
    click button    //input[@id='__appFind_textbox']/parent::td/following-sibling::td/button|//table[@id='__appFind_tbQuery1']//td/button[text()='查找']

Map_AppFind_高级查找
    [Arguments]    ${conditions}    ${isQueryInView}=No
    [Documentation]    *功能：*
    ...
    ...    高级查找
    ...
    ...    *参数说明：*
    ...
    ...    conditions : 查找条件，格式为 ：字段标签1=value1&字段标签2=value2&字段标签3=value3
    ...
    ...    isQueryInView ：是否视图内查询 Yes - 是, No - 否 , 默认值为No
    ...
    ...    *示例：*
    ...
    ...    Map_AppFind_高级查找 | 合同名称=公司库存合同-结算-工作流审批01&乙方单位=ZY乙方001&合同细类=工程类&签约日期 从=2014-01-07
    Return From Keyword If    '${conditions}'=='' or '${conditions}'=='[NULL]'
    click element    __appFind_switch_advanced
    ${fieldLabelArr}    ${valueArr}    _parseQueryStringToArray    ${conditions}
    run keyword if    '${isQueryInView}'=='Yes'    select checkbox    __appFind_queryinview
    Execute Javascript    var fieldLabels = ${fieldLabelArr};var values = ${valueArr};var table = window.document.getElementById('__appFind_tbQuery2');var tds = table.getElementsByTagName('td');var valueTd,tagName,firstChild;for(var i=0;i<fieldLabels.length;i++){for(var j=0;j<tds.length;j++){if(tds[j].innerText == fieldLabels[i]){valueTd = tds[j].nextSibling;firstChild = valueTd.firstChild;tagName = firstChild.tagName.toLowerCase();if(tagName == 'input'){firstChild.value = values[i];}else if(tagName == 'select'){var options = firstChild.getElementsByTagName('option');for(var k=0;k<options.length;k++){if(options[k].innerText == values[i]){options[k].selected = true;break;}}}else if(tagName == 'table'){firstChild.getElementsByTagName('input')[0].value = values[i];}break;}}}
    click button    //table[@id='__appFind_tbQuery2']//tr[last()]/td/button[text()='查找']
