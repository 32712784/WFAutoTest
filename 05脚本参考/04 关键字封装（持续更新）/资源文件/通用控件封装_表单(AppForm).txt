*** Settings ***
Library           Selenium2Library
Resource          通用操作封装.txt
Resource          通用控件封装_下拉框(SelectBox).txt

*** Keywords ***
Map_AppForm_切换横标签
    [Arguments]    ${tabName}
    [Documentation]    *功能：*
    ...
    ...    切换横标签
    ...
    ...    *参数说明：*
    ...
    ...    tabName ：横标签名称
    ...
    ...    *示例：*
    ...
    ...    Map_AppForm_切换横标签 | 基本资料
    Wait Until Page Contains Element    //td[@id='appForm_tabs']/span[text()='${tabName}']
    click element    //td[@id='appForm_tabs']/span[text()='${tabName}']

Map_AppForm_切换侧标签
    [Arguments]    ${leftBarName}
    [Documentation]    *功能：*
    ...
    ...    切换侧标签
    ...
    ...    *参数说明：*
    ...
    ...    leftBarName ：侧标签名称
    ...
    ...    *示例：*
    ...
    ...    Map_AppForm_切换侧标签 | 付款条件
    click element    //span[@id='appNavBar']/div[@title='${leftBarName}']

Map_AppForm_点击查找按钮
    [Arguments]    ${fieldName}    ${value}=1
    [Documentation]    *功能：*
    ...
    ...    点击字段右边的放大镜查找按钮
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：放大镜按钮对应的输入控件的name属性
    ...
    ...    value:默认值为1，也可以由数据驱动参数，为1则点击放大镜
    ...
    ...    *返回值说明：*
    ...
    ...    needNext：是否执行下一步
    ...
    ...    *示例：*
    ...
    ...    ${needNext} | Map_AppForm_点击查找按钮 | UseCostInfo
    ${needNext}    run keyword if    '${value}'=='' or '${value}'=='[NULL]'    Evaluate    0    ELSE    Evaluate
    ...    1
    Return From Keyword If    '${value}'=='' or '${value}'=='[NULL]'    ${needNext}
    ${disabled}    Get Element Attribute    //input[@name='${fieldName}']/parent::*/following-sibling::*/img|//input[@name='${fieldName}']/preceding-sibling::img@disabled
    ${needNext}    run keyword if    '${disabled}'=='disabled' or '${disabled}'=='true'    Evaluate    0    ELSE    Evaluate
    ...    1
    Run Keyword And Continue On Failure    run keyword if    '${needNext}'=='0'    fail    字段"${fieldName}"放大镜按钮禁用，无法点击
    Return From Keyword If    '${needNext}'=='0'    ${needNext}
    click image    //input[@name='${fieldName}' or @id='${fieldName}']/parent::*/following-sibling::*/img|//input[@name='${fieldName}' or @id='${fieldName}']/preceding-sibling::img
    [Return]    ${needNext}

Map_AppForm_点击命令按钮
    [Arguments]    ${menuName}
    [Documentation]    *功能：*
    ...
    ...    点击appform页面上排命令按钮
    ...
    ...    *参数说明：*
    ...
    ...    menuName ：需要点击的按钮名称；如有父子级联命令，父子命令之间用/分割
    ...
    ...    *示例：*
    ...
    ...    1. 单独命令 ：Map_AppForm_点击命令按钮 | 发起审核
    ...
    ...    2. 父子命令 ： Map_AppForm_点击命令按钮 | 赠送积分/看房积分
    sleep    1500ms
    ${menuArr}    evaluate    u'${menuName}'.split('/')
    ${menuArrLen}    evaluate    len(${menuArr})
    run keyword if    ${menuArrLen} == 1    _map_AppForm_点击命令按钮_单独命令    ${menuName}    ELSE    _map_AppForm_点击命令按钮_父子命令    ${menuArr}

Map_AppForm_点击菜单项
    [Arguments]    ${menuPath}
    [Documentation]    *功能：*
    ...
    ...    点击appform页面的菜单按钮
    ...
    ...    *参数说明：*
    ...
    ...    menuPath : 用"/"分割的菜单命令
    ...
    ...    *示例：*
    ...
    ...    Map_AppForm_点击菜单项 | 操作/验证预收款
    ${menuArr}    evaluate    u'${menuPath}'.split('/')
    ${action}    Get Element Attribute    //table[@className='mnubar']//tr/td[@className='icMenu']/span[text()='${menuArr[0]}']/table[@className='mnuList']//tr/td[text()='${menuArr[1]}']/parent::tr@act
    _runJSViaClickButton    ${action}

Map_AppForm_给字段赋值
    [Arguments]    ${fieldName}    ${value}
    [Documentation]    *功能：*
    ...
    ...    给表单字段赋值，对于给字段下拉框赋值，也支持
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：需要赋值的input控件的name属性
    ...
    ...    value ：字段对应的值
    ...
    ...    *示例：*
    ...
    ...    Map_AppForm_给字段赋值 | ContractName | 我的测试合同1
    Return From Keyword If    '${value}' == '[NULL]'
    ${isEditable}    Map_AppForm_检查字段是否可以编辑    ${fieldName}
    Run Keyword And Continue On Failure    run keyword if    '${isEditable}' == '0'    fail    封装提示信息：字段${fieldName}不可编辑
    Return From Keyword If    '${isEditable}' == '0'
    ${isShow}    Map_AppForm_检查字段是否显示    ${fieldName}
    Run Keyword And Continue On Failure    run keyword if    '${isShow}' == '0'    fail    封装提示信息：字段${fieldName}不可见
    Return From Keyword If    '${isShow}' == '0'
    ${cssClass}    Get Element Attribute    //input[@name='${fieldName}'] | //textarea[@name='${fieldName}']@className
    run keyword if    '${cssClass}' == 'selectBox'    Map_SelectBox_给下拉框赋值    ${fieldName}    ${value}    ELSE IF    '${cssClass}' == 'rad2'
    ...    _setAppForm_radio    ${fieldName}    ${value}    ELSE IF    '${cssClass}' == 'num'    _setAppForm_num
    ...    ${fieldName}    ${value}    ELSE IF    '${cssClass}' == 'dtm'    _setAppForm_dtm    ${fieldName}
    ...    ${value}    ELSE    _setAppForm_txt    ${fieldName}    ${value}

Map_AppForm_检查字段是否可以编辑
    [Arguments]    ${fieldName}
    [Documentation]    *功能：*
    ...
    ...    返回字段是否可以编辑
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：需要检查的控件的name属性
    ...
    ...    *返回值：*
    ...
    ...    isEditable ：1： 可编辑 0 ：只读
    ...
    ...
    ...    *示例：*
    ...
    ...    ${isEditable} | Map_AppForm_检查字段是否可以编辑 | ContractName
    #检查下拉框selectBox是否可以编辑
    ${cssClass}    Get Element Attribute    //input[@name='${fieldName}']|//textarea[@name='${fieldName}']@className
    ${isSelectBox}    Evaluate    'selectBox' in '${cssClass}'
    ${disabled}    run keyword if    '${isSelectBox}' == 'True'    Get Element Attribute    //input[@name='${fieldName}']/parent::*/following-sibling::*/img@disabled
    ${isEditable}    run keyword if    '${disabled}'=='disabled' or '${disabled}'=='true'    Evaluate    0    ELSE    Evaluate
    ...    1
    Return From Keyword If    '${isSelectBox}' == 'True'    ${isEditable}
    #检查一般文本框是否可以编辑
    ${readOnly}    Get Element Attribute    //input[@name='${fieldName}']|//textarea[@name='${fieldName}']@readOnly
    ${disabled}    Get Element Attribute    //input[@name='${fieldName}']|//textarea[@name='${fieldName}']@disabled
    ${isEditable}    run keyword if    '${readOnly}'=='readonly' or '${readOnly}'=='true' or '${disabled}'=='disabled' or '${disabled}'=='true'    Evaluate    0    ELSE    Evaluate
    ...    1
    [Return]    ${isEditable}

Map_AppForm_检查字段是否显示
    [Arguments]    ${fieldName}
    [Documentation]    *功能：*
    ...
    ...    获取字段显示或隐藏
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：需要检查的控件的name属性
    ...
    ...    *返回值：*
    ...
    ...    isShow ：1 ： 显示 0 ：隐藏
    ...
    ...
    ...    *示例：*
    ...
    ...    ${isShow} | Map_AppForm_检查字段是否显示 | ContractName
    ${isShow}    Execute Javascript    var fieldName='${fieldName}';var inputs=window.document.getElementsByName(fieldName);if(!inputs||inputs.length==0){throw new Error(0,'封装提示信息：字段"'+ fieldName+'"不存在，请检查参数是否正确');}var currentElement=inputs[0];while(currentElement.tagName.toLowerCase()!='html'){if(currentElement.style&&currentElement.style.display&&currentElement.style.display=='none'){return 0;}currentElement=currentElement.parentElement;}return 1;
    [Return]    ${isShow}

Map_AppForm_检查横标签是否显示
    [Arguments]    ${tabName}
    [Documentation]    *功能：*
    ...
    ...    检查横标签是否显示，或者隐藏
    ...
    ...    *参数说明：*
    ...
    ...    tabName ：横标签名称
    ...
    ...    *返回值：*
    ...
    ...    isShow ：1 ： 显示 0 ：隐藏
    ...
    ...    *示例：*
    ...
    ...    ${isShow} | Map_AppForm_检查横标签是否显示 | 基本资料
    ${isShow}    Execute Javascript    var tabName='${tabName}';var appformTabsTD=window.document.getElementById('appForm_tabs');var appformTabs=appformTabsTD.getElementsByTagName('span');for(var i=0;i<appformTabs.length;i++){if(appformTabs[i].innerText==tabName){return 1;}}return 0;
    [Return]    ${isShow}

Map_AppForm_检查侧标签是否显示
    [Arguments]    ${leftBarName}
    [Documentation]    *功能：*
    ...
    ...    检查侧标签是否显示
    ...
    ...    *参数说明：*
    ...
    ...    leftBarName ：侧标签名称
    ...
    ...    *返回值：*
    ...
    ...    isShow ：1 ： 显示 0 ：隐藏
    ...
    ...    *示例：*
    ...
    ...    ${isShow} | Map_AppForm_检查侧标签是否显示 | 付款条件
    ${isShow}    Execute Javascript    var leftBarName='${leftBarName}';var appNavBar=window.document.getElementById('appNavBar');var appNavBarItems=appNavBar.getElementsByTagName('div');for(var i=0;i<appNavBarItems.length;i++){if(appNavBarItems[i].title==leftBarName&&appNavBarItems[i].style&&appNavBarItems[i].style.display&&appNavBarItems[i].style.display=='none'){return 0;}}for(var i=0;i<appNavBarItems.length;i++){if(appNavBarItems[i].title==leftBarName){return 1;}}return 0;
    [Return]    ${isShow}

_map_AppForm_点击命令按钮_单独命令
    [Arguments]    ${menuName}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    点击appform页面上排命令按钮(单独命令按钮)
    ...
    ...    *参数说明：*
    ...
    ...    menuName ：需要点击的按钮名称
    : FOR    ${i}    IN RANGE    10
    \    ${local_status}    Run Keyword And Return Status    click element    xpath=(//table[@class='mnubar']//td[@class='icMenu']//span[@class='mnuBtn' and text()='${menuName}']/parent::span)[${i}+1]
    \    Exit For Loop If    '${local_status}' == 'True'

_map_AppForm_点击命令按钮_父子命令
    [Arguments]    ${menuArr}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    点击appform页面上排命令按钮(父子级联命令按钮)
    ...
    ...    *参数说明：*
    ...
    ...    menuArr ：需要点击的按钮名称父子按钮命令数组
    ${action}    Get Element Attribute    //table[@className='mnubar']//tr/td[@className='icMenu']//span[text()='${menuArr[0]}']/parent::span/table[@className='mnuList']//tr/td[text()='${menuArr[1]}']/parent::tr@act
    _runJSViaClickButton    ${action}

_setAppForm_txt
    [Arguments]    ${fieldName}    ${value}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    给表单文本框字段赋值
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：需要赋值的input控件的name属性
    ...
    ...    value ：文本框文本
    Execute Javascript    var fieldName='${fieldName}';var value='${value}';var childControl=eval('window.appForm.'+fieldName);if (childControl.style.display != "none") {childControl.focus();childControl.value = value;childControl.fireEvent('onblur');var onchange= childControl.getAttribute('onchange');if(onchange){childControl.fireEvent('onchange');}}

_setAppForm_radio
    [Arguments]    ${fieldName}    ${value}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    给表单单选框字段赋值
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：需要赋值的input控件的name属性
    ...
    ...    value ：选项文本
    ${id}    Get Element Attribute    //input[@name='${fieldName}']/parent::td/following-sibling::td/label[text()='${value}']@for
    click element    ${id}

_setAppForm_num
    [Arguments]    ${fieldName}    ${value}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    给表单数字型文本框字段赋值
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：需要赋值的input控件的name属性
    ...
    ...    value ：文本框文本
    Execute Javascript    var fieldName='${fieldName}';var value='${value}';var childControl=eval('window.appForm.'+fieldName);if (childControl.style.display != "none") {childControl.focus();childControl.value = value;childControl.fireEvent('onblur');var onchange= childControl.getAttribute('onchange');if(onchange){childControl.fireEvent('onchange');}}

_setAppForm_dtm
    [Arguments]    ${fieldName}    ${value}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    给表单日期型文本框字段赋值
    ...
    ...    *参数说明：*
    ...
    ...    fieldName ：需要赋值的input控件的name属性
    ...
    ...    value ：文本框文本
    Execute Javascript    var fieldName='${fieldName}';var value='${value}';var childControl=eval('window.appForm.'+fieldName); if (childControl.style.display != "none") {childControl.focus();childControl.value = value;childControl.fireEvent('onbeforedeactivate');childControl.fireEvent('onchange');}
