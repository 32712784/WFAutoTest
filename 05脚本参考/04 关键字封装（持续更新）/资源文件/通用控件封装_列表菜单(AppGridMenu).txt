*** Settings ***
Documentation     封装AppGridMenu控件的一些操作
Library           Selenium2Library
Resource          通用操作封装.txt

*** Keywords ***
Map_AppGridMenu_点击列表菜单项
    [Arguments]    ${menuPath}
    [Documentation]    *功能：*
    ...
    ...    点击列表上方菜单项
    ...
    ...    *参数说明：*
    ...
    ...    menuPath : 菜单路径，父菜单与子菜单之间用"/"分割
    ...
    ...    *示例：*
    ...
    ...    1. 独立菜单场景 ：Map_AppGridMenu_点击列表菜单项 | 新增小订
    ...
    ...    2. 父子菜单场景 ：Map_AppGridMenu_点击列表菜单项 | 新增/预计合同
    Return From Keyword If    '${menuPath}' == '' \ or \ '${menuPath}' == '[NULL]'
    ${menuArr}    evaluate    u'${menuPath}'.split('/')
    ${menuArrLen}    evaluate    len(${menuArr})
    Run Keyword If    ${menuArrLen} == 1    Click Element    xpath=//table[@class='mnubar']//td//span[text()='${menuPath}' and @class='mnuBtn']/parent::span[@class='menu']    ELSE    _handleMultiLevelMenu    ${menuArr}

_handleMultiLevelMenu
    [Arguments]    ${menuArr}
    [Documentation]    ====内部方法，不对外使用====
    ...
    ...    *功能：*
    ...
    ...    处理多级菜单
    ...
    ...    *参数说明：*
    ...
    ...    menuArr : 菜单路径,父子路径数组
    Wait Until Page Contains Element    //table[@className='mnubar']//tr/td//span[text()='${menuArr[0]}' and @class='mnuBtn']/parent::span/table[@className='mnuList']//tr/td[text()='${menuArr[1]}']/parent::tr    20s
    ${action}    Get Element Attribute    //table[@className='mnubar']//tr/td//span[text()='${menuArr[0]}' and @class='mnuBtn']/parent::span/table[@className='mnuList']//tr/td[text()='${menuArr[1]}']/parent::tr@action
    _runJSViaClickButton    ${action}
