*** Settings ***
Documentation     场景2
...               数据准备：
...               1、流程模板（A发起-A审批-A手动归档）
...               2、抄送人B
Resource          ../02关键字封装/关键字引用.txt
Library           Selenium2Library    timeout=20

*** Variables ***
${抄送人}            王胜成

*** Test Cases ***
01-新建流程并抄送给用户B
    [Documentation]    新建流程，并添加抄送人，发起后，流程监控验证状态为处理中
    登录    ${wf_url}    admin    1
    新建流程    ${ProcessModule_1}    ${抄送人}
    Wait Until Element Attribute Contains    css=.record-line.em.node-HandleText    text    AutoTest    #等待审批记录出现AutoTest
    ${ProcessName}    Get Text    id=js-title
    Set Global Variable    ${ProcessName}
    log    ${ProcessName}
    select window    Main
    校验流程状态    ${ProcessName}    处理中
    Close All Browsers

02-我办理的流程-抄送查询
    [Documentation]    被抄送人用户进入我办理的流程--抄送，验证抄送给我的流程
    登录    ${wf_url}    ${抄送人}    1
    我办理的流程查询    抄送    ${ProcessName}
    我发起和办理的流程查询校验    ${ProcessName}    1
    Close All Browsers

03-传阅流程
    [Documentation]    对流程进行传阅操作，并验证传阅成功
    登录    ${wf_url}    admin    1
    我发起的流程查询    处理中    ${ProcessName}
    点击元素    css=.layui-table-body.layui-table-main tr[data-index='0'] td[data-field='ProcessName'] a    #点击流程名称打开审批页面
    ${handle}    select window    NEW    #切换到新开窗口
    流程审批更多操作    传阅    admin
    Wait Until Element Attribute Contains    css=.layui-layer-content.layui-layer-padding    text    传阅成功
    Element Should Contain    css=.layui-layer-content.layui-layer-padding    传阅成功
    Close All Browsers

04-我办理的流程-传阅查询
    [Documentation]    被传阅用户进入我办理的流程--传阅，验证存在传阅给我的流程
    登录    ${wf_url}    admin    1
    我办理的流程查询    传阅    ${ProcessName}
    我发起和办理的流程查询校验    ${ProcessName}    1
    Close All Browsers

05-我办理的流程-全部
    [Documentation]    我办理的流程-全部，验证能在此列表搜索到此场景的流程
    登录    ${wf_url}    admin    1
    我办理的流程查询    全部    ${ProcessName}
    我发起和办理的流程查询校验    ${ProcessName}    1
    Close All Browsers

06-我发起的流程-全部
    [Documentation]    我发起的流程-全部，验证能在此列表搜索到此场景的流程
    登录    ${wf_url}    admin    1
    我发起的流程查询    全部    ${ProcessName}
    我发起和办理的流程查询校验    ${ProcessName}    1
    Close All Browsers
