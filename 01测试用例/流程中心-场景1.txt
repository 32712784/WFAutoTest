*** Settings ***
Documentation     场景1
...               数据准备：
...               流程模板（A发起-A审批-A手动归档）
Resource          ../02关键字封装/关键字引用.txt
Library           Selenium2Library    timeout=20

*** Variables ***

*** Test Cases ***
01-新建流程并保存草稿
    [Documentation]    新建流程并保存草稿
    登录    ${wf_url}    admin    1
    新建流程    ${ProcessModule_1}    SaveDraft=保存草稿
    点击元素    css=div[type='dialog'] .layui-layer-btn1
    Wait For Condition    return document.title == '流程草稿'
    Title Should Be    流程草稿
    ${ProcessName}    Get Text    id=js-processName
    ${ProcessName}    Replace String    ${ProcessName}    [    ${EMPTY}
    ${ProcessName}    Replace String    ${ProcessName}    ]    ${EMPTY}
    Set Global Variable    ${ProcessName}
    log    ${ProcessName}
    Close All Browsers
    [Teardown]

02-我发起的流程-草稿查询
    [Documentation]    验证保存草稿成功，并通过我发起的流程-草稿查询出来
    登录    ${wf_url}    admin    1
    我发起的流程查询    草稿    ${ProcessName}
    Element Text Should Be    css=.layui-table-body.layui-table-main tr[data-index='0'] td[data-field='ProcessName']    ${ProcessName}    #校验流程名称
    Close Browser

03-草稿发起
    [Documentation]    通过我发起的流程-草稿进入流程并发起，验证发起成功
    登录    ${wf_url}    admin    1
    我发起的流程查询    草稿    ${ProcessName}
    查询后的操作    发起
    发起操作
    Wait Until Element Attribute Contains    css=.record-line.em.node-HandleText    text    AutoTest    #等待审批记录出现AutoTest
    select window    Main
    校验流程状态    ${ProcessName}    处理中
    Close All Browsers

04-我发起的流程-处理中查询
    [Documentation]    通过我发起的流程--处理中，搜索上个用例发起的流程，校验搜索结果
    登录    ${wf_url}    admin    1
    我发起的流程查询    处理中    ${ProcessName}
    ${SearchCount}    Get Element Count    css=.layui-table-body.layui-table-main tr    #获取搜索结果数量
    Element Text Should Be    css=.layui-table-body.layui-table-main tr[data-index='0'] td[data-field='ProcessName']    ${ProcessName}    #校验流程名称
    Should Be Equal    '${SearchCount}'    '1'    #搜索结果只有一条
    Close All Browsers

05-我办理的流程-待办查询
    [Documentation]    通过我办理的流程--待办，搜索出有发起后的待办流程
    登录    ${wf_url}    admin    1
    我办理的流程查询    待办    ${ProcessName}
    ${SearchCount}    Get Element Count    css=.layui-table-body.layui-table-main tr    #获取搜索结果数量
    Element Text Should Be    css=.layui-table-body.layui-table-main tr[data-index='0'] td[data-field='ProcessName']    ${ProcessName}    #校验流程名称
    Should Be Equal    '${SearchCount}'    '1'    #搜索结果只有一条
    Close All Browsers

06-待办审批-同意
    [Documentation]    通过待办进入审批，同意，验证审批成功，流程监控状态为已通过
    登录    ${wf_url}    admin    1
    我办理的流程查询    待办    ${ProcessName}
    点击元素    css=.layui-table-body.layui-table-main tr[data-index='0'] td[data-field='ProcessName'] a
    ${handle}    select window    NEW    #切换到新开窗口
    流程审批操作    同意    审批同意    2    3
    select window    Main
    校验流程状态    ${ProcessName}    已通过
    Close All Browsers

07-我发起的流程-已通过查询
    [Documentation]    通过我发起的流程--已通过，验证上一步审批到归档步骤，状态为已通过
    登录    ${wf_url}    admin    1
    我发起的流程查询    已通过    ${ProcessName}
    我发起和办理的流程查询校验    ${ProcessName}    1
    Close All Browsers

08-我办理的流程-已办查询
    [Documentation]    进入我办理的流程--已办，验证审批后流程进入已办
    登录    ${wf_url}    admin    1
    我办理的流程查询    已办    ${ProcessName}
    我发起和办理的流程查询校验    ${ProcessName}    1
    Close All Browsers

09-归档审批
    [Documentation]    进行归档审批，验证流程监控状态为已归档
    登录    ${wf_url}    admin    1
    我办理的流程查询    待办    ${ProcessName}
    点击元素    css=.layui-table-body.layui-table-main tr[data-index='0'] td[data-field='ProcessName'] a
    ${handle}    select window    NEW    #切换到新开窗口
    流程审批操作    归档    归档同意    3    3
    select window    Main
    校验流程状态    ${ProcessName}    已归档
    Close All Browsers

10-我发起的流程-已归档查询
    [Documentation]    进入我发起的流程--已归档，验证上一步归档的流程在此列表
    登录    ${wf_url}    admin    1
    我发起的流程查询    已归档    ${ProcessName}
    我发起和办理的流程查询校验    ${ProcessName}    1
    Close All Browsers
