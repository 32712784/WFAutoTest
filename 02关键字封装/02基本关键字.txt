*** Settings ***
Library           Selenium2Library
Library           AutoItLibrary

*** Keywords ***
点击元素
    [Arguments]    ${locator}    ${timeout}=None    ${modifier}=False
    [Documentation]    *功能：*
    ...
    ...    等待元素显示后进行点击
    ...
    ...    *参数说明：*
    ...
    ...    locator : 元素对象
    ...
    ...    timeout ：超时时间，最长等待多久后超时失败
    ...
    ...    modifier : 添加键盘事件，详见click element
    ...
    ...    *示例：*
    ...
    ...    点击元素 | id=easy_search_btn | 10 | CTRL
    Wait Until Element Is Visible    ${locator}    ${timeout}
    Wait Until Element Is Not Visible    css=.mp-holder-grid-holder    #加载时的页面元素，有时候点击不到元素因为此元素在顶层，class="mp-holder-grid-holder row-11"
    Click Element    ${locator}    ${modifier}

等待加载图标显示后消失
    [Arguments]    ${locator}
    [Documentation]    *功能：*
    ...
    ...    等待加载图标显示后消失
    ...
    ...    *参数说明：*
    ...
    ...    locator：加载图标元素，菊花加载图标是css=div[type='loading']；白色背景是id=pageLoading
    ...
    ...    *示例：*
    ...
    ...    等待加载图标显示后消失 | css=div[type='loading']
    Wait Until Element Is Visible    ${locator}
    Wait Until Element Is Not Visible    ${locator}

输入
    [Arguments]    ${locator}    ${text}    ${timeout}=None
    [Documentation]    *功能：*
    ...
    ...    等待元素显示后进行输入
    ...
    ...    *参数说明：*
    ...
    ...    locator : 元素对象
    ...
    ...    text : 输入内容
    ...
    ...    timeout ：超时时间，最长等待多久后超时失败
    ...
    ...    *示例：*
    ...
    ...    输入 | id=easy_search_btn | admin | 10
    Wait Until Element Is Visible    ${locator}    ${timeout}
    Input Text    ${locator}    ${text}
